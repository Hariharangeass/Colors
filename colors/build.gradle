apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

group = 'com.marverenic.colors'
version = '1.1'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 25
        versionCode 2
        versionName "1.1"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:25.3.1'

    testCompile 'junit:junit:4.12'
    testCompile "com.google.truth:truth:0.33"
    testCompile 'org.mockito:mockito-core:2.8.9'
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    pkg {
        repo = 'Colors'
        name = 'com.marverenic.colors'

        version {
            name = '1.1'
            released  = new Date()
            vcsTag = 'v1.1'
        }

        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/marverenic/Colors'
        websiteUrl = 'https://github.com/marverenic/Colors'
    }
    configurations = ['archives']
}
task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    destinationDir = file("../javadoc/")
    failOnError false
}

task generateJavadocsJar(type: Jar) {
    from javadoc.destinationDir
    classifier 'javadoc'
    dependsOn javadoc
}

artifacts {
    archives generateJavadocsJar
    archives generateSourcesJar
}
